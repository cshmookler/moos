pkgname='moos-inotify-ipc'
pkgver=1.2.0
pkgrel=1
pkgdesc='A socketless inter-process communication library for Linux.  Uses inotify (https://en.wikipedia.org/wiki/Inotify) to exchange messages between programs.'
arch=('any')
license=('Zlib')
options=('!debug')
makedepends=(
    'git'
    'meson'
    'gtest'
    'moos-cpp-result'
)
source=('git+https://github.com/cshmookler/inotify_ipc.git')
sha256sums=('SKIP')
_repo='inotify_ipc'

pkgver() {
    cd "$_repo"
    cat "VERSION"
}

build() {
    cd "$_repo"
    arch-meson . build 
    meson compile -C build 
}

check() {
    cd "$_repo"
    meson test -C build 
}

package() {
    cd "$_repo"
    meson install -C build --destdir "$pkgdir"
    install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 "README.md" "$pkgdir/usr/share/doc/$pkgname/README.md"
}
