pkgname='moos-dwm'
pkgver=20250808
pkgrel=1
pkgdesc='Patched dynamic window manager for X.'
arch=('any')
url='https://tools.suckless.org/dwm/'
license=('MIT')
options=(
    'zipman'
    '!debug'
)
makedepends=('git')
depends=(
    'libx11'
    'libxinerama'
    'libxft'
    'dmenu'
    'moos-fonts'
    'moos-system-state'
    'moos-status-bar'
)
provides=('dwm')
conflicts=('dwm')
source=(
    'git+https://git.suckless.org/dwm'
    'dwm.c.patch'
    'config.mk.patch'
    'config.h'
)
sha256sums=(
    'SKIP'
    '2e773dd4f276e2485dd40dd13180322519503c5bea66c639115e39bc72033010'
    '8749849723cb05bcc0c3c34b189220767387c65039504432851ac33558a98bf6'
    '34f7d7d96582259139d6324b5bcfd3f3e4bfedac159ce75c23edaa3e3bc81b71'
)
_repo='dwm'

prepare() {
    cd "$_repo"
    patch < "$srcdir/dwm.c.patch"
    patch < "$srcdir/config.mk.patch"
    cp "$srcdir/config.h" .
}

build() {
    cd "$_repo"
    make
}

_backlight_service_wantedby='graphical.target'
_backlight_service() {
    echo '[Unit]'
    echo 'Description=Enable reading and writing to backlight brightness files on startup.'
    echo 'After=multi-user.target'
    echo ''
    echo '[Service]'
    echo $'ExecStart=/usr/bin/bash -c \'if test -n "$(ls -A \'/sys/class/backlight/\')"; then chmod 666 /sys/class/backlight/*/brightness; fi\''
    echo ''
    echo '[Install]'
    echo "WantedBy=$_backlight_service_wantedby"
}

package() {
    cd "$_repo"

    # Install this program
    make PREFIX="/usr" DESTDIR="$pkgdir/" install
    install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 "README" "$pkgdir/usr/share/doc/$pkgname/README"

    # Install and enable the systemd service
    _service_name="dwm-backlight.service"
    _service_src="$srcdir/$_service_name"
    _service_dest="$pkgdir/usr/lib/systemd/system/$_service_name"
    _service_symlink_dir="$pkgdir/etc/systemd/system/$_backlight_service_wantedby.wants/"
    _backlight_service > "$_service_src"
    install -Dm644 "$_service_src" "$_service_dest"
    install -dm755 "$_service_symlink_dir"
    ln -s "$_service_dest" "$_service_symlink_dir/$_service_name"
}
