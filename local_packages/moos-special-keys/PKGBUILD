pkgname='moos-special-keys'
pkgver=0.4.0
pkgrel=1
pkgdesc='Provides functions for common special (multimedia) keys. Communicates with status_bar (https://github.com/cshmookler/status_bar) for instant visual feedback.'
arch=('any')
license=('Zlib')
options=('!debug')
makedepends=(
    'git'
    'meson'
    'gtest'
    'argparse'
    'moos-cpp-result'
)
depends=(
    'moos-system-state'
    'moos-status-bar'
)
_service_name='special-keys-backlight'
source=(
    'git+https://github.com/cshmookler/special_keys.git'
    "$_service_name.service"
)
sha256sums=(
    'SKIP'
    'a916d9236b8d55618980f3502ea855a64aa1a2c7117762bbbbe9636b56701df4'
)
_repo='special_keys'

_install_hook() {
    echo "[Trigger]"
    echo "Operation = Install"
    echo "Type = Package"
    echo "Target = $pkgname"
    echo ""
    echo "[Action]"
    echo "Description = Enabling $_service_name service..."
    echo "When = PostTransaction"
    echo "Depends = systemd"
    echo "Exec = /usr/bin/systemctl enable $_service_name.service"
}

_removal_hook() {
    echo "[Trigger]"
    echo "Operation = Remove"
    echo "Type = Package"
    echo "Target = $pkgname"
    echo ""
    echo "[Action]"
    echo "Description = Disabling the $_service_name service..."
    echo "When = PreTransaction"
    echo "Depends = systemd"
    echo "Exec = /usr/bin/systemctl disable $_service_name.service"
}

pkgver() {
    cd "$_repo"
    cat "VERSION"
}

build() {
    cd "$_repo"
    arch-meson . build 
    meson compile -C build 
}

check() {
    cd "$_repo"
    meson test -C build 
}

package() {
    cd "$_repo"

    # Install this program.
    meson install -C build --destdir "$pkgdir"
    install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 "README.md" "$pkgdir/usr/share/doc/$pkgname/README.md"

    # Install the systemd service.
    install -Dm644 "$srcdir/$_service_name.service" "$pkgdir/etc/systemd/system/$_service_name.service"

    # Install the post-installation and pre-removal pacman hooks.
    INSTALL_HOOK="$pkgname-install.hook"
    REMOVAL_HOOK="$pkgname-removal.hook"
    _install_hook > "$INSTALL_HOOK"
    _removal_hook > "$REMOVAL_HOOK"
    install -Dm644 "$INSTALL_HOOK" "$pkgdir/etc/pacman.d/hooks/$INSTALL_HOOK"
    install -Dm644 "$REMOVAL_HOOK" "$pkgdir/etc/pacman.d/hooks/$REMOVAL_HOOK"
}
