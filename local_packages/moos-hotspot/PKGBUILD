pkgname='moos-hotspot'
pkgver=20250323
pkgrel=1
pkgdesc='Creates a WiFi hotspot (Access Point) that connects automatically.'
arch=('any')
options=('!debug')

_service_name="moos-hotspot"
_con_name="moos-hotspot"
_ssid="" # By default, use the hostname instead of a custom SSID.
_password=$(tr -dc A-Za-z0-9 </dev/urandom | head -c 10)

_hotspot_conf() {
    echo "[Unit]"
    echo "Description=Create a WiFi hotspot (Access Point) with NetworkManager"
    echo "Requires=NetworkManager.service"
    echo "After=NetworkManager.service"
    echo ""
    echo "[Service]"
    echo "Type=oneshot"
    echo "ExecStart=/usr/bin/env bash -c '(nmcli connection show \"$_con_name\" && nmcli connection delete \"$_con_name\") || true'"
    echo "ExecStart=/usr/bin/env bash -c 'nmcli device wifi hotspot con-name \"$_con_name\" ssid \$((test -s /etc/$pkgname/ssid && cat /etc/$pkgname/ssid) || cat /etc/hostname) password \$(cat /etc/$pkgname/password)'"
    echo "ExecStart=/usr/bin/env bash -c 'nmcli connection modify \"$_con_name\" connection.autoconnect yes'"
    echo "ExecStart=/usr/bin/env bash -c 'nmcli connection modify \"$_con_name\" connection.autoconnect-priority 999'"
    echo "ExecStart=/usr/bin/env bash -c 'nmcli connection modify \"$_con_name\" ipv4.addresses 10.0.0.1/24'"
    echo "ExecStart=/usr/bin/env bash -c 'nmcli connection up \"$_con_name\"'"
    echo ""
    echo "[Install]"
    echo "WantedBy=multi-user.target"
}

_install_hook() {
    echo "[Trigger]"
    echo "Operation = Install"
    echo "Type = Package"
    echo "Target = $pkgname"
    echo ""
    echo "[Action]"
    echo "Description = Enabling $_service_name service..."
    echo "When = PostTransaction"
    echo "Depends = systemd"
    echo "Exec = /usr/bin/systemctl enable $_service_name.service"
}

_removal_hook() {
    echo "[Trigger]"
    echo "Operation = Remove"
    echo "Type = Package"
    echo "Target = $pkgname"
    echo ""
    echo "[Action]"
    echo "Description = Disabling the $_service_name service..."
    echo "When = PreTransaction"
    echo "Depends = systemd"
    echo "Exec = /usr/bin/systemctl disable $_service_name.service"
}

package() {
    # Install the systemd service that creates and updates the hotspot.
    _hotspot_conf > "$_service_name.service"
    install -Dm644 "$_service_name.service" "$pkgdir/etc/systemd/system/$_service_name.service"

    # Create the file that contains the hotspot SSID.
    # If this file is empty or missing, the hostname is the hotspot SSID.
    printf "$_ssid" > "ssid"
    install -Dm644 "ssid" "$pkgdir/etc/$pkgname/ssid"

    # Create the file that contains the hotspot password.
    printf "$_password" > "password"
    # Only root can read this file.
    install -Dm600 "password" "$pkgdir/etc/$pkgname/password"

    # Install the post-installation and pre-removal pacman hooks.
    INSTALL_HOOK="$pkgname-install.hook"
    REMOVAL_HOOK="$pkgname-removal.hook"
    _install_hook > "$INSTALL_HOOK"
    _removal_hook > "$REMOVAL_HOOK"
    install -Dm644 "$INSTALL_HOOK" "$pkgdir/etc/pacman.d/hooks/$INSTALL_HOOK"
    install -Dm644 "$REMOVAL_HOOK" "$pkgdir/etc/pacman.d/hooks/$REMOVAL_HOOK"
}
