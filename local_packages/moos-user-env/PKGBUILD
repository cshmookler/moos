pkgname='moos-user-env'
pkgver=20250113
pkgrel=1
pkgdesc='Default user environment for MOOS.'
arch=('any')
options=('!debug')
depends=(
    'xorg-xinit'
    'xorg-xset'
    'physlock'
    'xss-lock'
    'qt5-styleplugins'
    'qt6gtk2'
    'breeze-gtk'
    'python'
    'curl'
    'rsync'
    'moos-dwm'
    'moos-fonts'
    'moos-slock'
    'moos-status-bar'
)
source=(
    '.bash_profile'
    '.bashrc'
    '.xinitrc'
    '.gtkrc-2.0'
    'gtk3-settings.ini'
    'gtk4-settings.ini'
    'tb-user.js'
    'ff-policies.json'
    'ff_extensions.py'
    'reset_user_env.py'
    'reset_firefox_policies.py'
)
sha256sums=(
    '1469da7b27766ad4d07eb202de0142609a72763f5e3088d67893334d68725984'
    '61934632ed4a70fc85dbc4bf22aad3bf3344fa4d23e4ef58c51ec504f288e4c0'
    'a2b934394ce932b50bba5560295725b171cc781834b3a8617b8cade884b39e49'
    '18fc70fc443362ec32257804ceb561a3ae3eaff9a39e01ee79c9f115303c3001'
    'b845d6d8a1b817f323a7e884e9973c4a50fd1aeffd2e88d5b014b599920be956'
    'b845d6d8a1b817f323a7e884e9973c4a50fd1aeffd2e88d5b014b599920be956'
    '6dde9eece3fa18197ecdf08122527160f4e525d69b804c06aa150a642769fb1d'
    'a38a89aceed7e8e328db1d41768bea2b40f6b0044fba5ef51131f49a340fe21e'
    '630b43b8cfb026cc24810a06c4a28d8b07108f172dc69d95e4206fbad2233d4d'
    'a15d160ee294bd4190a0cde4d82892ad022e89a3cec981181666ac479eebc3c4'
    'e93098bd1262ba8df74645b44942cf65ee92a2a26f1c4708f99b6a8c3cf6097f'
)

build() {
    python ff_extensions.py "$srcdir/ff-policies.json" "$srcdir/firefox"
}

package() {
    _factory="$pkgdir/etc/user_env"

    _env="$_factory/env"
    install -Dm644 ".bash_profile" "$_env/.bash_profile"
    install -Dm644 ".bashrc" "$_env/.bashrc"
    install -Dm644 ".xinitrc" "$_env/.xinitrc"
    install -Dm644 ".gtkrc-2.0" "$_env/.gtkrc-2.0"
    install -Dm644 "gtk3-settings.ini" "$_env/.config/gtk-3.0/settings.ini"
    install -Dm644 "gtk4-settings.ini" "$_env/.config/gtk-4.0/settings.ini"

    _tb="$_env/.local/share/torbrowser/tbb/x86_64/tor-browser/Browser/TorBrowser/Data/Browser/profile.default"
    install -Dm644 "tb-user.js" "$_tb/user.js"

    _ff="$_factory/firefox"
    rsync --recursive --mkpath "--chmod=D755,F644" "firefox/" "$_ff/"

    install -Dm755 "reset_user_env.py" "$pkgdir/usr/bin/reset_user_env"
    install -Dm755 "reset_firefox_policies.py" "$pkgdir/usr/bin/reset_firefox_policies"
}
