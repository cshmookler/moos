pkgname='moos-user-env'
pkgver=20240916
pkgrel=1
pkgdesc='Default user environment for MOOS.'
arch=('any')
groups=('moos')
options=('!debug')
makedepends=(
    'python'
    'curl'
    'unzip'
)
depends=(
    'xorg-xinit'
    'xorg-xset'
    'physlock'
    'xss-lock'
    'qt5-styleplugins'
    'qt6gtk2'
    'breeze-gtk'
    'rsync'
    'firefox'
    'moos-dwm'
    'moos-fonts'
    'moos-slock'
    'moos-status-bar'
)
source=(
    '.bash_profile'
    '.bashrc'
    '.xinitrc'
    '.gtkrc-2.0'
    'gtk3-settings.ini'
    'gtk4-settings.ini'
    'firefox-user.js'
    'tor-browser-user.js'
    'reset_user_env.py'
    'ff_addons.py'
)
sha256sums=(
    '1469da7b27766ad4d07eb202de0142609a72763f5e3088d67893334d68725984'
    '47ec861cf7fbbeb9851607fc95e6a6288813dec65421c9e756c2fa85042ccc24'
    '1e87c4c5d74fe53108c628222a7a471829eebccbcfcdf74d91746bc815b40958'
    '18fc70fc443362ec32257804ceb561a3ae3eaff9a39e01ee79c9f115303c3001'
    'b845d6d8a1b817f323a7e884e9973c4a50fd1aeffd2e88d5b014b599920be956'
    'b845d6d8a1b817f323a7e884e9973c4a50fd1aeffd2e88d5b014b599920be956'
    'f61cf6ce1e4a232c285b32a0aaa6d22e6da4341aa2337c247da26fb971e7ad65'
    '6dde9eece3fa18197ecdf08122527160f4e525d69b804c06aa150a642769fb1d'
    '74265be3807b6f448d21212675a938df15131c69683c51d360d7228ac83419f5'
    '89f4f672eb799ae316720ca7737a50d51e7863ff9cd0a937f6e20c2f52270f2f'
)
_env_name="env"
_ff_name="firefox"

build() {
    python ff_addons.py "$srcdir/$_ff_name/extensions"
}

package() {
    _factory="$pkgdir/etc/user_env"

    _env="$_factory/$_env_name"
    install -Dm644 ".bash_profile" "$_env/.bash_profile"
    install -Dm644 ".bashrc" "$_env/.bashrc"
    install -Dm644 ".xinitrc" "$_env/.xinitrc"
    install -Dm644 ".gtkrc-2.0" "$_env/.gtkrc-2.0"
    install -Dm644 "gtk3-settings.ini" "$_env/.config/gtk-3.0/settings.ini"
    install -Dm644 "gtk4-settings.ini" "$_env/.config/gtk-4.0/settings.ini"
    install -Dm644 "tor-browser-user.js" "$_env/.local/share/torbrowser/tbb/x86_64/tor-browser/Browser/TorBrowser/Data/Browser/profile.default/user.js"

    _ff="$_factory/$_ff_name"
    rsync --recursive --mkpath --ignore-existing "--chmod=D755,F644" "firefox/" "$_ff/"
    install -Dm644 "firefox-user.js" "$_ff/user.js"

    install -Dm755 "reset_user_env.py" "$pkgdir/usr/bin/reset_user_env"
}
