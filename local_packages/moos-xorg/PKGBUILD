pkgname='moos-xorg'
pkgver=20260105
pkgrel=1
pkgdesc='All required packages for a graphical MOOS installation with Xorg.'
arch=('any')
options=('!debug')
makedepends=(
    'git'
    'rsync'
)
depends=(
    # Base packages
    'moos'

    # Display server
    $(pacman -Sgq 'xorg')         # X server and related utilities
    'xorg-xinit'                  # Allow users to manually start the X server
    'xss-lock'                    # Set the active screen locker
    'moos-slock'                  # Screen locker
    'moos-us-keys-xorg'           # Custom US-style keyboard layout for Xorg

    # Window manager
    'moos-dwm'                    # Window manager (runs under the X server)
    'moos-system-state'           # System state fetcher and modifier
    'moos-status-bar'             # Status bar for DWM

    # Notifications
    'libnotify'                   # Library for sending desktop notifications
    'dunst'                       # Notification server
    
    # Themes
    'breeze-gtk'                  # Breeze theme for GTK
    'breeze-icons'                # Icons for Breeze
    'qt5-styleplugins'            # Use GTK styles in QT5 applications
    'qt6gtk2'                     # Use GTK styles in QT6 applications

    # Fonts
    'noto-fonts'                  # Google fonts
    'noto-fonts-emoji'            # Google color emojis
    'noto-color-emoji-fontconfig' # Always default to Google's color emojis
    'noto-fonts-cjk'              # Google chinese, japanese, and korean fonts
    'noto-fonts-extra'            # Google extra fonts
    'ttf-hack-nerd'               # Monospace font for coding
    'ttf-liberation'              # Free alternatives to certain MS fonts
    'ttf-ms-win11'                # MS fonts from Win11

    # Applications and Utilities
    'moos-st'                     # Terminal emulator
    'tree-sitter-cli'             # Advanced syntax highlighting for Neovim
    'ffmpeg'                      # AV manipulation + player (ffplay)
    'mpv'                         # AV dedicated player
    'firefox'                     # Normal browser
    'torbrowser-launcher'         # TOR browser
    'libreoffice-still'           # Office Suite
    'moos-snap'                   # Simple screenshot utility
    'moos-neovim-conf'            # Neovim + my preferred configuration

    # Virtual machine
    'qemu-full'                   # Virtual machine (QEMU)
    'libvirt'                     # Wrapper for QEMU
    'virt-manager'                # GUI launcher for QEMU
    'virt-viewer'                 # Advanced VM viewer for QEMU
)
source=(
    '10-secure.conf'
    '20-qol.conf'
    'bashrc_xorg.sh'
    'bash_profile_xorg.sh'
    '.xinitrc'
    '.gtkrc-2.0'
    'gtk3-settings.ini'
    'gtk4-settings.ini'
    'dunstrc'
    'notify-low-battery.c'
    'tb-user.js'
    'ff-policies.json'
    'ff_extensions.py'
)
sha256sums=(
    'e5d244ba8652ceda9db4aff11c63b74478367fe681daee53fdf5435c3e5be1bc'
    '106a3154f1942daa9fd9f451baab3fef71ac245b525d8d5d681296ff15ef1e5b'
    '56dbff9298b788ddc303cb72c905009c77769628d9a48162f0407c675aa974b7'
    '39fa171a0de2a4a38bab6fdd3589a636d9a8613e1423b94d2bffda692f43f43a'
    '28a18fce961f873d62d2d48d9be4e86e8408bc3c9415a2e9012c7fc26190f263'
    '18fc70fc443362ec32257804ceb561a3ae3eaff9a39e01ee79c9f115303c3001'
    'b845d6d8a1b817f323a7e884e9973c4a50fd1aeffd2e88d5b014b599920be956'
    'b845d6d8a1b817f323a7e884e9973c4a50fd1aeffd2e88d5b014b599920be956'
    '666a345ba416c74674f550aab4d0bdd76173d434bbe5711aa507ecdd9e1dd821'
    '8b9bebf13c037b83ea2f0609e5ec5bf46958779d2c515354e7d7fba1f42f0978'
    '2dde5b36a023113cb10bbbd11256dbab36968c459438bcb9ddde5f1d1ac8f8ed'
    'a38a89aceed7e8e328db1d41768bea2b40f6b0044fba5ef51131f49a340fe21e'
    'f3ad9a3a105b8fac962983e590f7632613ea6871f4bcb6c50dafa53df5deea7d'
)

pkgver() {
    date +%Y%m%d
}

build() {
    python ff_extensions.py "$srcdir/ff-policies.json" "$srcdir/firefox"
    gcc "$srcdir/notify-low-battery.c" -lsystem_state -o "$srcdir/notify-low-battery"
}

package() {
    install -Dm644 "10-secure.conf" "$pkgdir/etc/X11/xorg.conf.d/10-secure.conf"
    install -Dm644 "20-qol.conf" "$pkgdir/etc/X11/xorg.conf.d/20-qol.conf"

    rsync --recursive --mkpath "--chmod=D755,F644" "firefox/" "$pkgdir/etc/firefox/"

    _skel="$pkgdir/etc/moos-skel"
    install -Dm644 "bashrc_xorg.sh" "$_skel/.bashrc.d/xorg.sh"
    install -Dm644 "bash_profile_xorg.sh" "$_skel/.bash_profile.d/xorg.sh"
    install -Dm644 ".xinitrc" "$_skel/.xinitrc"
    install -Dm644 ".gtkrc-2.0" "$_skel/.gtkrc-2.0"
    install -Dm644 "gtk3-settings.ini" "$_skel/.config/gtk-3.0/settings.ini"
    install -Dm644 "gtk4-settings.ini" "$_skel/.config/gtk-4.0/settings.ini"
    install -Dm644 "dunstrc" "$_skel/.config/dunst/dunstrc"
    install -Dm644 "tb-user.js" "$_skel/.local/share/torbrowser/tbb/x86_64/tor-browser/Browser/TorBrowser/Data/Browser/profile.default/user.js"

    install -Dm755 "notify-low-battery" "$pkgdir/usr/bin/notify-low-battery"
}
