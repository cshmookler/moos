pkgname='moos-xorg'
pkgver=20250811
pkgrel=1
pkgdesc='All required packages for a graphical MOOS installation with Xorg.'
arch=('any')
options=('!debug')
makedepends=(
    'git'
    'rsync'
    'aspell'
    'aspell-en' # English is the default language.
)
depends=(
    # Base packages
    'moos'

    # Display server
    $(pacman -Sgq 'xorg')         # X server and related utilities
    'xorg-xinit'                  # Allow users to manually start the X server
    'xss-lock'                    # Set the active screen locker
    'xsel'                        # Manipulate the X selection
    'moos-slock'                  # Screen locker
    'moos-us-keys-xorg'           # Custom US-style keyboard layout for Xorg

    # Window manager
    'moos-dwm'                    # Window manager (runs under the X server)
    'moos-system-state'           # System state fetcher and modifier
    'moos-status-bar'             # Status bar for DWM

    # Notifications
    'libnotify'                  # Library for sending desktop notifications
    'dunst'                       # Notification server
    
    # Themes
    'breeze-gtk'                  # Breeze theme for GTK
    'breeze-icons'                # Icons for Breeze
    'qt5-styleplugins'            # Use GTK styles in QT5 applications
    'qt6gtk2'                     # Use GTK styles in QT6 applications

    # Fonts
    'noto-fonts'                  # Google fonts
    'noto-fonts-emoji'            # Google color emojis
    'noto-color-emoji-fontconfig' # Always default to Google's color emojis
    'noto-fonts-cjk'              # Google chinese, japanese, and korean fonts
    'noto-fonts-extra'            # Google extra fonts
    'ttf-hack-nerd'               # Monospace font for coding
    'ttf-liberation'              # Free alternatives to certain MS fonts
    'ttf-ms-win11-auto'           # MS fonts from Win11

    # Applications and Utilities
    'moos-st'                     # Terminal emulator
    'neovim'                      # Text editor and IDE
    'neovim-symlinks'             # System-wide aliases for Neovim
    'tree-sitter-cli'             # Advanced syntax highlighting for Neovim
    'ffmpeg'                      # AV manipulation + player (ffplay)
    'mpv'                         # AV dedicated player
    'firefox'                     # Normal browser
    'torbrowser-launcher'         # TOR browser
    'libreoffice-still'           # Office Suite
    'moos-snap'                   # Simple screenshot utility
    'tinyxxd'                    # Binary file viewer

    # Virtual machine
    'qemu-full'                   # Virtual machine (QEMU)
    'libvirt'                     # Wrapper for QEMU
    'virt-manager'                # GUI launcher for QEMU
    'virt-viewer'                 # Advanced VM viewer for QEMU

    # Language Server Protocol
    'vscode-html-languageserver' # HTML LSP
    'vscode-json-languageserver' # JSON LSP
    'vscode-css-languageserver'  # CSS LSP
    'typescript-language-server' # JS/TS LSP
    'yaml-language-server'       # YAML LSP
    'clang'                      # C++ LSP
    'lua-language-server'        # Lua LSP
    'vim-language-server'        # VimScript LSP
    'jedi-language-server'       # Python LSP
    'bash-language-server'       # Bash LSP
    'rust-analyzer'              # Rust LSP
    'texlab'                     # LaTeX LSP
    'csharp-ls'                  # C# LSP
    'openscad-lsp'               # OpenSCAD LSP
    'verible-bin'                # SystemVerilog LSP
    
    # Formatters
    'python-black'               # Python Formatter

    # Linters
    'mypy'                       # Python static typing linter
    'flake8'                     # Python style linter
    'usort'                      # Python import sorter

    # Software Development Kits
    'dotnet-sdk-bin'             # C# .NET SDK

    # LaTeX
    'texlive-basic'
    'texlive-latex'
    'texlive-latexrecommended'
    'texlive-fontsrecommended'
    'texlive-mathscience'
)
source=(
    '10-secure.conf'
    '20-qol.conf'
    '.shprofile'
    '.shrc'
    '.bash_profile'
    '.bashrc'
    '.zprofile'
    '.zshrc'
    '.xinitrc'
    '.gtkrc-2.0'
    'gtk3-settings.ini'
    'gtk4-settings.ini'
    'dunstrc'
    'notify-low-battery.c'
    'tb-user.js'
    'ff-policies.json'
    'ff_extensions.py'
    'git+https://github.com/cshmookler/neovim_conf.git'
)
sha256sums=(
    'e5d244ba8652ceda9db4aff11c63b74478367fe681daee53fdf5435c3e5be1bc'
    '106a3154f1942daa9fd9f451baab3fef71ac245b525d8d5d681296ff15ef1e5b'
    'afc0818d08c5d0a2d9dcf6f36c9a60e5f936460f7f27491abe9d87d72de610dd'
    '960c3b6c7a2e47fe7a8fb3a65b23ee7d57e8f1c8289ee4efb94f749c8178de29'
    'e149407c2bee17779caec70a7edd3d0000d172e7e4347429b80cb4d55bcec9c2'
    'a7f973bf90df9f9a89d40dc0bb19f36dad94e3ee8ebfa6c4afa6cccf09081446'
    'd9a356718338071158ea6b6bf6fd37f42cc3ee663df03370d4c7e48e293ef9a2'
    '9b7d925f390c690c560bb9f2f7e204204ad0be79d9c29b8ef786ed67a5e468c9'
    '6dc9f5cf4acbbe822a2321de025114d33b9dee293751d1c78fbddb1983e7abd6'
    '18fc70fc443362ec32257804ceb561a3ae3eaff9a39e01ee79c9f115303c3001'
    'b845d6d8a1b817f323a7e884e9973c4a50fd1aeffd2e88d5b014b599920be956'
    'b845d6d8a1b817f323a7e884e9973c4a50fd1aeffd2e88d5b014b599920be956'
    '666a345ba416c74674f550aab4d0bdd76173d434bbe5711aa507ecdd9e1dd821'
    '8b9bebf13c037b83ea2f0609e5ec5bf46958779d2c515354e7d7fba1f42f0978'
    '2dde5b36a023113cb10bbbd11256dbab36968c459438bcb9ddde5f1d1ac8f8ed'
    'a38a89aceed7e8e328db1d41768bea2b40f6b0044fba5ef51131f49a340fe21e'
    'f3ad9a3a105b8fac962983e590f7632613ea6871f4bcb6c50dafa53df5deea7d'
    'SKIP'
)

pkgver() {
    date +%Y%m%d
}

build() {
    python ff_extensions.py "$srcdir/ff-policies.json" "$srcdir/firefox"
    aspell --lang=en dump master | aspell --lang=en expand > "$srcdir/dict"
    gcc "$srcdir/notify-low-battery.c" -lsystem_state -o "$srcdir/notify-low-battery"
}

package() {
    install -Dm644 "10-secure.conf" "$pkgdir/etc/X11/xorg.conf.d/10-secure.conf"
    install -Dm644 "20-qol.conf" "$pkgdir/etc/X11/xorg.conf.d/20-qol.conf"

    rsync --recursive --mkpath "--chmod=D755,F644" "firefox/" "$pkgdir/etc/firefox/"

    _skel="$pkgdir/etc/moos-skel"
    install -Dm644 ".shprofile" "$_skel/.shprofile"
    install -Dm644 ".shrc" "$_skel/.shrc"
    install -Dm644 ".bash_profile" "$_skel/.bash_profile"
    install -Dm644 ".bashrc" "$_skel/.bashrc"
    install -Dm644 ".zprofile" "$_skel/.zprofile"
    install -Dm644 ".zshrc" "$_skel/.zshrc"
    install -Dm644 ".xinitrc" "$_skel/.xinitrc"
    install -Dm644 ".gtkrc-2.0" "$_skel/.gtkrc-2.0"
    install -Dm644 "gtk3-settings.ini" "$_skel/.config/gtk-3.0/settings.ini"
    install -Dm644 "gtk4-settings.ini" "$_skel/.config/gtk-4.0/settings.ini"
    install -Dm644 "dunstrc" "$_skel/.config/dunst/dunstrc"
    install -Dm644 "dict" "$_skel/.local/share/nvim/dict"
    install -Dm644 "tb-user.js" "$_skel/.local/share/torbrowser/tbb/x86_64/tor-browser/Browser/TorBrowser/Data/Browser/profile.default/user.js"
    rsync --recursive --mkpath "--chmod=D755,F644" "neovim_conf/" "$_skel/.config/nvim/"

    install -Dm755 "notify-low-battery" "$pkgdir/usr/bin/notify-low-battery"
}
