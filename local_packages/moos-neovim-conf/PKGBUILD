pkgname='moos-neovim-conf'
pkgver=20250222
pkgrel=1
pkgdesc='Default Neovim configuration for MOOS.'
arch=('any')
options=('!debug')
makedepends=(
    'git'
    'aspell'
    'aspell-en' # English is the default language.
)
depends=(
    # Required packages
    'neovim'
    'neovim-symlinks'
    'tree-sitter-cli'
    'xsel'
    'moos-user-env'
    'rsync'

    # Language Server Protocol
    'vscode-html-languageserver' # HTML LSP
    'vscode-json-languageserver' # JSON LSP
    'vscode-css-languageserver'  # CSS LSP
    'typescript-language-server' # JS/TS LSP
    'yaml-language-server'       # YAML LSP
    'clang'                      # C++ LSP
    'lua-language-server'        # Lua LSP
    'vim-language-server'        # VimScript LSP
    'jedi-language-server'       # Python LSP
    'bash-language-server'       # Bash LSP
    'rust-analyzer'              # Rust LSP
    'texlab'                     # LaTeX LSP
    'csharp-ls'                  # C# LSP
    'openscad-lsp'               # OpenSCAD LSP
    'verible-bin'                # SystemVerilog LSP
    
    # Formatters
    'python-black'               # Python Formatter

    # Linters
    'mypy'                       # Python Linter
    'flake8'                     # Python Linter
    'usort'                      # Python Linter

    # Software Development Kits
    'dotnet-sdk'                 # C# .NET SDK

    # LaTeX
    'texlive-basic'
    'texlive-latex'
    'texlive-latexrecommended'
    'texlive-fontsrecommended'
    'texlive-mathscience'

    # Other
    'tinyxxd'                    # Binary file viewer
    'npm'                        # Package manager for JacaScript
)
source=(
    'git+https://github.com/cshmookler/neovim_conf.git'
)
sha256sums=(
    'SKIP'
)
_repo='neovim_conf'

pkgver() {
    cd "$_repo"
    git log -1 --date=format:"%Y%m%d" --format="%ad"
}

build() {
    aspell --lang=en dump master | aspell --lang=en expand > "$srcdir/dict"
}

package() {
    _env="$pkgdir/etc/user_env/env"
    _conf="$_env/.config/nvim"
    install -Dm644 "dict" "$_env/.local/share/nvim/dict"
    install -Dm644 "$_repo/init.lua" "$_conf/init.lua"
    install -Dm644 "$_repo/lazy-lock.json" "$_conf/lazy-lock.json"
    rsync --recursive --mkpath "--chmod=D755,F644" "$_repo/lua/" "$_conf/lua/"
    install -Dm644 "$_repo/README.md" "$pkgdir/usr/share/doc/$pkgname/README.md"
}
